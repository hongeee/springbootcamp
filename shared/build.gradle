plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.openapi.generator'
    id 'com.rivancic.asyncapi-gradle-plugin'
    id 'io.freefair.lombok'
}

version = '0.0.1-SNAPSHOT'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen'

    // Bean 유효성 검사
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // open-api-spec support packages
    implementation 'io.springfox:springfox-swagger2:3.0.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5'
    // OpenApi Bug workaround @see https://github.com/OpenAPITools/openapi-generator/issues/3668
    implementation 'io.swagger:swagger-annotations:1.5.17'
}

bootJar {
    enabled = false
}

task redoc(type:Exec) {
    // For this to work correctly, `yarn global add redoc`
    commandLine 'npx', 'redoc-cli', 'bundle', "${projectDir}/src/main/resources/api.yml", '-o', "${buildDir}/redoc.html"
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "${projectDir}/src/main/resources/api.yml".toString()
    outputDir = "${buildDir}/openapi".toString()
    apiPackage = 'com.vroong.shared.rest'
    modelPackage = 'com.vroong.shared.rest'
    modelNameSuffix = 'Dto'
    apiFilesConstrainedTo = ['']
    modelFilesConstrainedTo = ['']
    supportingFilesConstrainedTo = ['ApiUtil.java']
    configOptions = [
            delegatePattern: 'true',
            title: 'order',
            useTags: 'true',
            dateLibrary: 'java8',
            java8: 'true',
            hideGenerationTimestamp: 'true'
    ]
    validateSpec = true
}

sourceSets {
    main {
        java {
            srcDir file("${project.buildDir.path}/openapi/src/main/java")
        }
    }
}

compileJava.dependsOn('openApiGenerate')

asyncapi {
    // Prerequisite: brew install node
    version = '1.9.14'
    asyncapi = 'src/main/resources/asyncapi.yml'
    template = '@asyncapi/html-template'
    output = "build/asyncapi"
    forceWrite = true
    parameters = [
        'singleFile': 'true'
    ]
}
